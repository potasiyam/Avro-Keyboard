{
  =============================================================================
  This Source Code Form is subject to the terms of the Mozilla Public
  License, v. 2.0. If a copy of the MPL was not distributed with this
  file, You can obtain one at https://mozilla.org/MPL/2.0/.
  =============================================================================
}

{$INCLUDE ../ProjectDefines.inc}
{ COMPLETE TRANSFERING! }

unit ufrmAvroMouse;

interface

uses
  Windows,
  Messages,
  SysUtils,
  Variants,
  Classes,
  Graphics,
  Controls,
  Forms,
  Dialogs,
  StdCtrls,
  Buttons,
  ExtCtrls,
  System.Skia;

type
  TfrmAvroMouse = class(TForm)
    Panel1: TPanel;
    BitBtn3: TBitBtn;
    BitBtn1: TBitBtn;
    BitBtn2: TBitBtn;
    BitBtn4: TBitBtn;
    BitBtn5: TBitBtn;
    BitBtn6: TBitBtn;
    BitBtn7: TBitBtn;
    BitBtn8: TBitBtn;
    BitBtn9: TBitBtn;
    BitBtn10: TBitBtn;
    BitBtn11: TBitBtn;
    BitBtn12: TBitBtn;
    BitBtn13: TBitBtn;
    BitBtn14: TBitBtn;
    BitBtn15: TBitBtn;
    BitBtn16: TBitBtn;
    BitBtn17: TBitBtn;
    BitBtn18: TBitBtn;
    BitBtn19: TBitBtn;
    BitBtn20: TBitBtn;
    BitBtn21: TBitBtn;
    BitBtn24: TBitBtn;
    BitBtn25: TBitBtn;
    BitBtn28: TBitBtn;
    BitBtn29: TBitBtn;
    BitBtn22: TBitBtn;
    BitBtn23: TBitBtn;
    BitBtn26: TBitBtn;
    BitBtn27: TBitBtn;
    BitBtn30: TBitBtn;
    BitBtn31: TBitBtn;
    BitBtn32: TBitBtn;
    BitBtn33: TBitBtn;
    BitBtn34: TBitBtn;
    BitBtn35: TBitBtn;
    BitBtn36: TBitBtn;
    BitBtn37: TBitBtn;
    BitBtn38: TBitBtn;
    BitBtn39: TBitBtn;
    Panel2: TPanel;
    BitBtn40: TBitBtn;
    BitBtn42: TBitBtn;
    BitBtn43: TBitBtn;
    BitBtn44: TBitBtn;
    BitBtn45: TBitBtn;
    BitBtn46: TBitBtn;
    BitBtn47: TBitBtn;
    BitBtn48: TBitBtn;
    BitBtn49: TBitBtn;
    BitBtn50: TBitBtn;
    BitBtn51: TBitBtn;
    BitBtn52: TBitBtn;
    BitBtn53: TBitBtn;
    BitBtn54: TBitBtn;
    BitBtn55: TBitBtn;
    BitBtn56: TBitBtn;
    BitBtn57: TBitBtn;
    BitBtn58: TBitBtn;
    BitBtn59: TBitBtn;
    BitBtn60: TBitBtn;
    BitBtn61: TBitBtn;
    Panel3: TPanel;
    BitBtn62: TBitBtn;
    BitBtn63: TBitBtn;
    BitBtn64: TBitBtn;
    BitBtn65: TBitBtn;
    BitBtn66: TBitBtn;
    BitBtn67: TBitBtn;
    BitBtn68: TBitBtn;
    BitBtn69: TBitBtn;
    BitBtn70: TBitBtn;
    BitBtn71: TBitBtn;
    GroupBox1: TGroupBox;
    BitBtn72: TBitBtn;
    BitBtn73: TBitBtn;
    BitBtn74: TBitBtn;
    BitBtn75: TBitBtn;
    BitBtn76: TBitBtn;
    BitBtn77: TBitBtn;
    BitBtn78: TBitBtn;
    BitBtn79: TBitBtn;
    BitBtn80: TBitBtn;
    BitBtn81: TBitBtn;
    BitBtn82: TBitBtn;
    BitBtn83: TBitBtn;
    BitBtn84: TBitBtn;
    BitBtn85: TBitBtn;
    BitBtn86: TBitBtn;
    BitBtn87: TBitBtn;
    BitBtn88: TBitBtn;
    GroupBox2: TGroupBox;
    But_KSs: TBitBtn;
    But_NGK: TBitBtn;
    But_NgG: TBitBtn;
    But_JNYA: TBitBtn;
    But_NYAC: TBitBtn;
    But_NYACh: TBitBtn;
    But_NYAJ: TBitBtn;
    But_T_T: TBitBtn;
    But_SsNYA: TBitBtn;
    But_HM: TBitBtn;
    But_ND: TBitBtn;
    But_ZFola: TBitBtn;
    But_RFola: TBitBtn;
    BitBtn102: TBitBtn;
    BitBtn103: TBitBtn;
    BitBtn104: TBitBtn;
    BitBtn105: TBitBtn;
    BitBtn106: TBitBtn;
    btnTab: TButton;
    btnBackspace: TButton;
    btnSpace: TButton;
    btnEnter: TButton;
    BitBtn89: TBitBtn;
    BitBtn90: TBitBtn;
    procedure FormClose(Sender: TObject; var Action: TCloseAction);
    procedure BitBtn1Click(Sender: TObject);
    procedure btnTabClick(Sender: TObject);
    procedure But_KSsClick(Sender: TObject);
    procedure But_NGKClick(Sender: TObject);
    procedure But_NgGClick(Sender: TObject);
    procedure But_JNYAClick(Sender: TObject);
    procedure But_NYACClick(Sender: TObject);
    procedure But_NYAChClick(Sender: TObject);
    procedure But_NYAJClick(Sender: TObject);
    procedure But_T_TClick(Sender: TObject);
    procedure But_SsNYAClick(Sender: TObject);
    procedure But_HMClick(Sender: TObject);
    procedure But_NDClick(Sender: TObject);
    procedure But_RFolaClick(Sender: TObject);
    procedure But_ZFolaClick(Sender: TObject);
    procedure FormCreate(Sender: TObject);
    private
      { Private declarations }
      procedure TypeIt(const tStr: string);
      procedure DetectLeftClickOnTitleBar(var Msg: TWMNCLButtonDown); message WM_NCLBUTTONDOWN;
      procedure DetectRightClickOnTitleBar(var Msg: TWMNCLButtonDown); message WM_NCRBUTTONDOWN;
      procedure RegenerateGlyphs;
      procedure RenderGlyph(const ABtn: TBitBtn);
      procedure AssignSvgGlyph(const ABtn: TBitBtn; const ASvgPath: string);
      function DisplayTextOf(const ABtn: TBitBtn): string;
      function ThemeColor(const ASysColor: TColor): TColor;
      function MouseGlyphDir: string;
      function AssetMouseGlyphDir: string;
    public
      { Public declarations }
    protected
      procedure CreateParams(var Params: TCreateParams); override;
  end;

var
  frmAvroMouse: TfrmAvroMouse;

implementation

{$R *.dfm}

uses
  uForm1,
  BanglaChars,
  KeyboardFunctions,
  uWindowHandlers,
  uRegistrySettings,
  clsLayout,
  WindowsDarkMode,
  Vcl.Themes,
  System.Types,
  System.RegularExpressions;

const
  Show_Window_in_Taskbar = True;

procedure TfrmAvroMouse.BitBtn1Click(Sender: TObject);
var
  WC: Char;
begin
  WC := Char((Sender as TBitBtn).Tag);
  TypeIt(WC);
end;

procedure TfrmAvroMouse.btnTabClick(Sender: TObject);
begin
  SendKey_SendInput((Sender as TButton).Tag);
end;

procedure TfrmAvroMouse.But_HMClick(Sender: TObject);
begin
  TypeIt(b_H + b_Hasanta + b_M);
end;

procedure TfrmAvroMouse.But_JNYAClick(Sender: TObject);
begin
  TypeIt(b_J + b_Hasanta + b_NYA);
end;

procedure TfrmAvroMouse.But_KSsClick(Sender: TObject);
begin
  TypeIt(b_K + b_Hasanta + b_Ss);
end;

procedure TfrmAvroMouse.But_NDClick(Sender: TObject);
begin
  TypeIt(b_Nn + b_Hasanta + b_Dd);
end;

procedure TfrmAvroMouse.But_NgGClick(Sender: TObject);
begin
  TypeIt(b_NGA + b_Hasanta + b_G);
end;

procedure TfrmAvroMouse.But_NGKClick(Sender: TObject);
begin
  TypeIt(b_NGA + b_Hasanta + b_K);
end;

procedure TfrmAvroMouse.But_NYACClick(Sender: TObject);
begin
  TypeIt(b_NYA + b_Hasanta + b_C);
end;

procedure TfrmAvroMouse.But_NYAChClick(Sender: TObject);
begin
  TypeIt(b_NYA + b_Hasanta + b_CH);
end;

procedure TfrmAvroMouse.But_NYAJClick(Sender: TObject);
begin
  TypeIt(b_NYA + b_Hasanta + b_J);
end;

procedure TfrmAvroMouse.But_RFolaClick(Sender: TObject);
begin
  TypeIt(b_Hasanta + b_R);
end;

procedure TfrmAvroMouse.But_SsNYAClick(Sender: TObject);
begin
  TypeIt(b_Ss + b_Hasanta + b_Nn);
end;

procedure TfrmAvroMouse.But_T_TClick(Sender: TObject);
begin
  TypeIt(b_T + b_Hasanta + b_T);
end;

procedure TfrmAvroMouse.But_ZFolaClick(Sender: TObject);
begin
  TypeIt(b_Hasanta + b_Z);
end;

procedure TfrmAvroMouse.CreateParams(var Params: TCreateParams);
begin
  inherited CreateParams(Params);
  with Params do
  begin
    if Show_Window_in_Taskbar then
    begin
      ExStyle := ExStyle or WS_EX_APPWINDOW and not WS_EX_TOOLWINDOW;
      ExStyle := ExStyle or WS_EX_TOPMOST or WS_EX_NOACTIVATE;
      WndParent := GetDesktopwindow;
    end
    else if not Show_Window_in_Taskbar then
    begin
      ExStyle := ExStyle and not WS_EX_APPWINDOW;
    end;
  end;
end;

procedure TfrmAvroMouse.DetectLeftClickOnTitleBar(var Msg: TWMNCLButtonDown);
begin
  if (Msg.HitTest = htCaption) then
    SetForegroundWindow(Self.Handle);
  inherited;
end;

procedure TfrmAvroMouse.DetectRightClickOnTitleBar(var Msg: TWMNCLButtonDown);
begin
  if (Msg.HitTest = htCaption) then
    SetForegroundWindow(Self.Handle);
  inherited;
end;

procedure TfrmAvroMouse.FormClose(Sender: TObject; var Action: TCloseAction);
begin
  if Self.Top >= 0 then
    AvroMousePosX := intToStr(Self.Top);
  if Self.Left >= 0 then
    AvroMousePosY := intToStr(Self.Left);

  Action := caFree;

  frmAvroMouse := nil;
end;

procedure TfrmAvroMouse.FormCreate(Sender: TObject);
begin
  try
    if (StrToInt(AvroMousePosX) > Screen.Width) or (StrToInt(AvroMousePosX) < 0) then
      AvroMousePosX := '0';
    if (StrToInt(AvroMousePosY) > Screen.Height) or (StrToInt(AvroMousePosY) < 0) then
      AvroMousePosY := '0';

    Self.Top := StrToInt(AvroMousePosX);
    Self.Left := StrToInt(AvroMousePosY);
  except
    on E: Exception do
    begin
      AvroMousePosX := '0';
      AvroMousePosY := '0';
      Self.Top := 0;
      Self.Left := 0;
    end;
  end;

  RegenerateGlyphs;
end;

function TfrmAvroMouse.DisplayTextOf(const ABtn: TBitBtn): string;
begin
  if (ABtn.Tag = $200C) or (ABtn.Tag = $200D) then
    Result := ''
  else if ABtn.Tag <> 0 then
    Result := WideChar(ABtn.Tag)
  else if ABtn.Name = 'But_KSs' then
    Result := b_K + b_Hasanta + b_Ss
  else if ABtn.Name = 'But_NGK' then
    Result := b_NGA + b_Hasanta + b_K
  else if ABtn.Name = 'But_NgG' then
    Result := b_NGA + b_Hasanta + b_G
  else if ABtn.Name = 'But_JNYA' then
    Result := b_J + b_Hasanta + b_NYA
  else if ABtn.Name = 'But_NYAC' then
    Result := b_NYA + b_Hasanta + b_C
  else if ABtn.Name = 'But_NYACh' then
    Result := b_NYA + b_Hasanta + b_CH
  else if ABtn.Name = 'But_NYAJ' then
    Result := b_NYA + b_Hasanta + b_J
  else if ABtn.Name = 'But_T_T' then
    Result := b_T + b_Hasanta + b_T
  else if ABtn.Name = 'But_SsNYA' then
    Result := b_Ss + b_Hasanta + b_Nn
  else if ABtn.Name = 'But_HM' then
    Result := b_H + b_Hasanta + b_M
  else if ABtn.Name = 'But_ND' then
    Result := b_Nn + b_Hasanta + b_Dd
  else if ABtn.Name = 'But_ZFola' then
    Result := b_Hasanta + b_Z
  else if ABtn.Name = 'But_RFola' then
    Result := b_Hasanta + b_R
  else
    Result := '';
end;

function TfrmAvroMouse.ThemeColor(const ASysColor: TColor): TColor;
begin
  Result := TStyleManager.ActiveStyle.GetSystemColor(ASysColor);
end;

function TfrmAvroMouse.MouseGlyphDir: string;
begin
  Result := ExtractFilePath(ParamStr(0)) + 'mouse-glyphs\';
end;

function TfrmAvroMouse.AssetMouseGlyphDir: string;
begin
  Result := ExtractFilePath(ParamStr(0)) + '..\assets\mouse-glyphs\';
end;

procedure TfrmAvroMouse.AssignSvgGlyph(const ABtn: TBitBtn; const ASvgPath: string);
const
  MaskColor = clFuchsia;
var
  W, H: Integer;
  InkC, FaceC: TColor;
  InR, InG, InB: Byte;
  FaR, FaG, FaB: Byte;
  DOM: ISkSVGDOM;
  Surface: ISkSurface;
  Pixmap: ISkPixmap;
  X, Y, Alpha: Integer;
  Px: PByte;
  KP: PByteArray;
  Key: TBitmap;
  RootW, RootH, FitScale: Single;

  function LeadingNumber(const AValue: string; out ANumber: Single): Boolean;
  var
    M: TMatch;
  begin
    M := TRegEx.Match(AValue, '\s*([0-9]*\.?[0-9]+)');
    Result := M.Success and
      TryStrToFloat(M.Groups[1].Value, ANumber, TFormatSettings.Invariant);
  end;

  { True when the root <svg> tag carries explicit width/height attributes:
    such documents render at a fixed size and ignore the container size. }
  function TryRootSize(out AWidth, AHeight: Single): Boolean;
  var
    Svg: TStringList;
    Tag, WStr, HStr: string;

    function AttrValue(const ATag, AName: string): string;
    var
      M: TMatch;
    begin
      Result := '';
      M := TRegEx.Match(ATag, '\b' + AName + '\s*=\s*["'']([^"'']+)');
      if M.Success then
        Result := M.Groups[1].Value;
    end;

  begin
    Result := False;
    AWidth := 0;
    AHeight := 0;
    Svg := TStringList.Create;
    try
      try
        Svg.LoadFromFile(ASvgPath, TEncoding.UTF8);
      except
        Svg.Text := '';
      end;
      Tag := TRegEx.Match(Svg.Text, '<svg\b[^>]*').Value;
      if Tag = '' then
        Exit;
      WStr := AttrValue(Tag, 'width');
      HStr := AttrValue(Tag, 'height');
      { Percentages are relative sizes, not fixed ones. }
      if (Pos('%', WStr) > 0) or (Pos('%', HStr) > 0) then
        Exit;
      Result := LeadingNumber(WStr, AWidth) and LeadingNumber(HStr, AHeight) and
        (AWidth > 0) and (AHeight > 0);
    finally
      Svg.Free;
    end;
  end;

begin
  W := ABtn.ClientWidth;
  H := ABtn.ClientHeight;
  if (W <= 0) or (H <= 0) then
    Exit;

  DOM := TSkSVGDOM.MakeFromFile(ASvgPath);
  if DOM = nil then
    Exit;

  InkC := ThemeColor(clBtnText);
  InR := GetRValue(InkC);  InG := GetGValue(InkC);  InB := GetBValue(InkC);
  FaceC := ThemeColor(clBtnFace);
  FaR := GetRValue(FaceC);  FaG := GetGValue(FaceC);  FaB := GetBValue(FaceC);

  Surface := TSkSurface.MakeRaster(W, H);
  Surface.Canvas.Clear($00000000);
  DOM.SetContainerSize(TSizeF.Create(W, H));

  { Fixed-size SVGs ignore the container and would render at design size
    pinned to the top-left corner (visible as misalignment at high DPI).
    Fit and center them on the canvas so they behave like viewBox-only
    SVGs, which Skia scales and centers by itself. }
  if TryRootSize(RootW, RootH) then
  begin
    FitScale := W / RootW;
    if H / RootH < FitScale then
      FitScale := H / RootH;
    Surface.Canvas.Save;
    try
      Surface.Canvas.Translate((W - RootW * FitScale) / 2, (H - RootH * FitScale) / 2);
      Surface.Canvas.Scale(FitScale, FitScale);
      DOM.Render(Surface.Canvas);
    finally
      Surface.Canvas.Restore;
    end;
  end
  else
    DOM.Render(Surface.Canvas);

  Pixmap := Surface.PeekPixels;
  if Pixmap = nil then
    Exit;

  Key := TBitmap.Create;
  try
    Key.PixelFormat := pf32bit;
    Key.Width       := W;
    Key.Height      := H;
    Key.Canvas.Brush.Color := MaskColor;
    Key.Canvas.FillRect(Rect(0, 0, W, H));

    for Y := 0 to H - 1 do
    begin
      KP := Key.ScanLine[Y];
      for X := 0 to W - 1 do
      begin
        Px := PByte(Pixmap.GetPixelAddr(X, Y));
        Alpha := PByte(NativeUInt(Px) + 3)^;
        if Alpha = 0 then
        begin
          KP[X * 4]     := $FF;
          KP[X * 4 + 1] := 0;
          KP[X * 4 + 2] := $FF;
        end
        else
        begin
          KP[X * 4]     := (Alpha * InB + (255 - Alpha) * FaB) div 255;
          KP[X * 4 + 1] := (Alpha * InG + (255 - Alpha) * FaG) div 255;
          KP[X * 4 + 2] := (Alpha * InR + (255 - Alpha) * FaR) div 255;
        end;
      end;
    end;

    Key.Canvas.Pixels[0, H - 1]     := MaskColor;
    Key.Canvas.Pixels[W - 1, H - 1] := MaskColor;
    Key.Canvas.Pixels[0, 0]         := MaskColor;

    ABtn.Glyph.Assign(Key);
    ABtn.Glyph.Transparent      := True;
    ABtn.Glyph.TransparentColor := MaskColor;
  finally
    Key.Free;
  end;
end;

procedure TfrmAvroMouse.RenderGlyph(const ABtn: TBitBtn);
var
  Path: string;
begin
  if (ABtn.ClientWidth <= 0) or (ABtn.ClientHeight <= 0) then
    Exit;

  { The dfm carries a Bangla caption per button for design-time readability;
    clear it so the runtime shows only the SVG glyph. }
  ABtn.Caption := '';

  Path := MouseGlyphDir + ABtn.Name + '.svg';
  if not FileExists(Path) then
    Path := AssetMouseGlyphDir + ABtn.Name + '.svg';
  if FileExists(Path) then
    AssignSvgGlyph(ABtn, Path);
end;


procedure TfrmAvroMouse.RegenerateGlyphs;
var
  I: Integer;
  Btn:  TBitBtn;
  Display: string;
begin
  for I := 0 to ComponentCount - 1 do
    if Components[I] is TBitBtn then
    begin
      Btn     := TBitBtn(Components[I]);
      Display := DisplayTextOf(Btn);
      if Display <> '' then
        RenderGlyph(Btn);
    end;
end;

procedure TfrmAvroMouse.TypeIt(const tStr: string);
begin
  SendKey_Char(tStr);
end;

end.
